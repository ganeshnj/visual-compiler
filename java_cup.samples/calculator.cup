package calculator;

import java.util.TreeMap;
import java.util.Map;
import java.util.ArrayList;

parser code {:
    
    public void report_error(String message, Object info) {
        StringBuffer m = new StringBuffer("Error");
		m.append (info.toString());
        m.append(" : "+message);
        System.err.println(m.toString());
    }
   
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

	private Map<String, Integer> values = new TreeMap<String, Integer> ();

	public void setValue (String name, int value) {
		values.put(name, new Integer (value));
		System.out.println (name);
	}
	
	public int getValue (String name) {
		int value = 0;
		if (values.containsKey(name))
			value = values.get(name).intValue();
		return value;
	}
	
	private Container container = null;
	public Container getContainer() {
		return container;
	}
	
	public void setContainer(Container container) {
		this.container = container;
	}
:}

terminal			EQUAL;
terminal			PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN;
terminal			EOLN, TERM;
terminal Integer	NUMBER;
terminal String		ID;
terminal			UPLUS, UMINUS;

terminal String		CLASSNAME;
terminal			CREATE, CLASS, METHOD;
terminal			DRAW;
terminal			SET, POSITION, RET, ARG, ACC, WIDTH, INHERIT, HAS;
terminal			ADD, TO;
terminal			PUBLIC, PROTECTED, PRIVATE;
terminal			DATA, TYPE;
terminal			ONE, MANY;
   
nonterminal 		commands, definitions, definition, instruction;
nonterminal Integer expr, qty;
nonterminal String	accID;
nonterminal 		creator, classSetter, methodSetter, dataSetter;

precedence left		PLUS, MINUS;
precedence left		TIMES, DIVIDE;
precedence right	UPLUS, UMINUS;

commands	::=	definitions
			;
		
definitions	::= 
			|	definitions	definition
			;
			
definition	::=	instruction TERM
			;

instruction	::=	ID:n EQUAL expr:e				{: setValue (n, e); :}
			|	expr:e							{: System.out.println (e); :}
			|	CREATE creator
			|	CLASS classSetter
			|	METHOD methodSetter
			|	DATA dataSetter
			|	DRAW ID:cn						{: getContainer().getClass(cn).draw(); :}
			;
			
creator		::= CLASS ID:cn				{: getContainer().addClass(cn); :}
			|	METHOD ID:mn				{: getContainer().addMethod(mn); :}
			| 	DATA ID:dn				{: getContainer().addData(dn); :}
			;
		
classSetter ::= ID:cn SET POSITION NUMBER:x NUMBER:y 	{: getContainer().getClass(cn).setPosition(x, y); :} 
 			|	ID:cn SET ACC accID:ai	{: getContainer().getClass(cn).setAccessLevel(ai); :} 
 			|	ID:cn SET WIDTH NUMBER:n	{: getContainer().getClass(cn).setWidth(n); :} 
			|	ID:cn INHERIT ID:cn2 	{: getContainer().getClass(cn).setParent(getContainer().getClass(cn2)); :}
			|	ID:cn HAS qty:q ID:cn2	{: getContainer().getClass(cn).addRelationship(getContainer().getClass(cn2), q); :}
			;
			
methodSetter ::= ID:mn ADD TO ID:cn 		{: getContainer().getClass(cn).addMethod(getContainer().getMethod(mn)); :}
			|	ID:mn SET RET ID:type	{: getContainer().getMethod(mn).setReturnType(type); :}
			|	ID:mn SET ARG ID:an ID:type	{: getContainer().getMethod(mn).addArgument(an, type); :}
			|	ID:mn SET ACC accID:ai	{: getContainer().getMethod(mn).setAccessLevel(ai); :}
			;

dataSetter ::= 	ID:dn SET TYPE ID:type		{: getContainer().getData(dn).setType(type); :}
			|	ID:dn SET ACC accID:ai		{: getContainer().getData(dn).setAccessLevel(ai); :}
			|	ID:dn ADD TO ID:cn			{: getContainer().getClass(cn).addData(getContainer().getData(dn)); :}
			;

accID ::= PUBLIC		{: RESULT = "+"; :}
		| PROTECTED		{: RESULT = "#"; :}	
		| PRIVATE		{: RESULT = "-"; :}
		;

qty 	::= ONE			{: RESULT = 1; :}		
		| MANY			{: RESULT = 99; :}
		;
		
expr	::=	expr:e1	PLUS	expr:e2	{: RESULT = e1 + e2; :}
		|	expr:e1	MINUS	expr:e2	{: RESULT = e1 - e2; :}
		|	expr:e1	TIMES	expr:e2	{: RESULT = e1 * e2; :}
		|	expr:e1	DIVIDE	expr:e2	{: RESULT = e1 / e2; :}
		|	MINUS	expr:e			{: RESULT = - e; :} %prec UMINUS
		|	PLUS	expr:e			{: RESULT = + e; :} %prec UPLUS
		|	LPAREN	expr:e	RPAREN	{: RESULT = e; :}
		|	NUMBER:n				{: RESULT = n; :}
		|	ID:n					{: RESULT = getValue(n); :}
		;
